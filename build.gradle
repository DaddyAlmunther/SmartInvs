group 'fr.minuskube.inv'
version '1.2.9'

apply plugin: 'java'
apply plugin: 'maven'

sourceCompatibility = 1.8

javadoc {
    options.links(
            'https://hub.spigotmc.org/javadocs/spigot/',
            'https://docs.oracle.com/javase/8/docs/api/'
    )

    options.addStringOption('Xdoclint:none', '-quiet')
}

task javadocJar(type: Jar) {
    archiveClassifier = 'javadoc'
    from javadoc
}

task sourcesJar(type: Jar) {
    archiveClassifier = 'sources'
    from sourceSets.main.allSource
}

repositories {
    mavenCentral()

    maven { url 'https://hub.spigotmc.org/nexus/content/repositories/snapshots/' }
    maven { url 'https://oss.sonatype.org/content/repositories/snapshots' }
}

dependencies {
    compileOnly 'org.spigotmc:spigot-api:1.8.8-R0.1-SNAPSHOT'
}

jar {
    archiveFileName = 'SmartInvs-' + project.version + '.jar'
}

artifacts {
    archives jar, javadocJar, sourcesJar
}

task uploadToLocalMaven(type: Upload) {
    configuration = configurations.archives
    repositories {
        mavenDeployer {
            repository(url: "file://${buildDir}/localMaven")
        }
    }
}

uploadToLocalMaven.dependsOn jar, javadocJar, sourcesJar,  install
uploadArchives.dependsOn uploadToLocalMaven
